apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.konduto.sdk'
version = '2.0.1'

sourceCompatibility = JavaVersion.VERSION_1_7

compileJava {
    targetCompatibility = JavaVersion.VERSION_1_7
}


repositories {
	mavenCentral()
}

dependencies {
    compile 'commons-httpclient:commons-httpclient:3.1'
    compile 'com.google.code.gson:gson:2.2.4'
    testCompile 'junit:junit:4.11'
    testCompile 'com.github.tomakehurst:wiremock:1.18'
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Konduto\'s Java SDK'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Konduto\'s JAVA SDK'
                url 'http://konduto.com'

                scm {
                    connection 'https://github.com/konduto/java-sdk'
                    developerConnection 'https://github.com/konduto/java-sdk'
                    url 'https://github.com/konduto/java-sdk'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'rsampaio'
                        name 'Raphael Sampaio'
                        email 'raphael@konduto.com'
                    }
                }
            }
        }
    }
}